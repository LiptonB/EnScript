class MainClass;

class FindEvidenceDialogClass: DialogClass {
  MainClass Main;
  PathEditClass EvidenceDirectoryEdit;
  ListEditClass ListEdit;
  DirRootClass DirRoot;

  FindEvidenceDialogClass(DialogClass parent, MainClass main) :
    DialogClass(parent, "Evidence Selection Dialog"),
    Main = main,
    EvidenceDirectoryEdit(this, "Evidence directory", START, START, 200, 12, 0, main.EvidenceDirectory, FOLDEROPEN),
    ListEdit(this, "Files in directory", START, NEXT, 200, 75, 0, main.FileList, ContextClass::CANCHECK),
    DirRoot(LocalMachine)
  {}

  virtual void ChildEvent(const EventClass &evt) {
    DialogClass::ChildEvent(evt);
    if (EvidenceDirectoryEdit.Matches(evt)) {
      String newPath = EvidenceDirectoryEdit.GetText();
      DirectoryClass dir();
      NameListClass FileList();
      if (DirRoot.GetListing(newPath, dir)) {
        foreach (DirectoryClass file in dir) {
          new NameListClass(FileList, file.FullPath());
        }
      }
      ListEdit.SetDataRoot(FileList);
      Main.FileList = FileList;
    }
  }
}

class MainClass {
  String EvidenceDirectory;
  NameListClass FileList;

  MainClass() :
    FileList()
  {}

  void Main(CaseClass c) {

    FindEvidenceDialogClass diag(null, this);

    if (!c) {
      diag.ErrorMessage("Please open a case first");
      return;
    }

    if (diag.Execute() == SystemClass::OK) {
      Console.WriteLine("Path selected: " + EvidenceDirectory);
      foreach(NodeClass node in FileList) {
        if (node.IsSelected()) {
          Console.WriteLine("Selected File: " + node.Name());
        }
      }
    }

    foreach (NodeClass node in FileList) {
      if (node.IsSelected()) {
        String filename = EvidenceDirectory +
          (EvidenceDirectory.SubString(EvidenceDirectory.GetLength() - 1, 1) == "\\" ? "" : "\\") +
          node.Name();
        EvidenceClass ev(null, node.Name());
        EvidencePathClass evPath(ev.EvidencePathRoot(), filename);
        EvidenceOpenClass evOpen();
        String extension = filename.GetFileExt();
        if (extension == "img") {
          ev.SetOpenMode(EvidenceClass::OPENRAWIMAGE);
        } else if (extension == "E01") {
          ev.SetOpenMode(EvidenceClass::OPENEVIDENCEFILE);
        } else {
          Console.WriteLine("Skipping " + filename + " - unknown extension");
          continue;
        }
        if (ev.AddToCase(c, evOpen)) {
          Console.WriteLine("Added " + filename + " to case successfully");
        } else {
          Console.WriteLine("Could not add " + filename + " to case");
        }
      }
    }
  }
}

//This Code will search for the files with the extention of SPL and export the file to c:\temo directory, if the file content is ANSI the code will also display that on console
/*
class MainClass {
  void Main(CaseClass c) {
    int notWorks;
    uint opts;
    String outPath = "C:\\temp\\";
    EntryFileClass ef();
    uint count;
    for(ItemIteratorClass i(c); EntryClass e = i.GetNextEntry();) {
      if (e.Extension() == "SPL") {
        if (ef.Open(e, opts)) {
          String contents;
          ef.SetCodePage(CodePageClass::ANSI);
          ef.ReadString(contents);
          Console.WriteLine("file contents");
          Console.WriteLine(contents);
          Console.WriteLine("*************");
          LocalFileClass lf();
          String name = outPath + e.Name() + "_" + count;
          if (lf.Open(name, FileClass::WRITE)) {
            lf.WriteBuffer(ef);
            Console.WriteLine("File Created:" + name);
            count++;
            lf.Close();
          } else
            notWorks++;
          ef.Close();
        }
        else {
          Console.WriteLine("Could Not Open " + e.FullPath());
          notWorks++;
        }
      }
    }
  }
}
*/
//this function will create hash from selected files and put them in c:\temp
/*
class MainClass {
  void Main(CaseClass c) {
    Hash2LibraryClass::Hash2LibraryBuilderClass builder();
    //change C:\temp to the directory you want the HashLibrary stored
    if (builder.Open("C:\\temp\\", Hash2LibraryClass::Hash2LibraryBuilderClass::OT_CREATE)) {
      Hash2SetClass newSet("Interesting Files");
      if (builder.AddNewHashSet(newSet)) { // add the new hash set to the LibraryBuilder
        HashClass md5();
        SHA1Class sha1();
        for(ItemIteratorClass iter(c, 0, ItemIteratorClass::CURRENTVIEW_SELECTED); EntryClass e = iter.GetNextEntry();) {
          //populate the hash set
          Hash2ItemClass newItem();
          EntryFileClass ef();
          ef.Open(e);
          //compute hash values
          ef.ComputeMD5(md5);
          ef.ComputeSHA1(sha1);
          //set the hashes in the Hash2ItemClass
          newItem.SetMD5(md5);
          newItem.SetSHA1(sha1);
          newItem.SetLogicalSize(e.LogicalSize());
          //add new item to the set
          newSet.Add(builder, newItem);
        }
      }
    }
  }
}
*/

//This functionw will look for assigned file in the registry
/*
class MainClass {

  void RunRegistry(CaseClass c, EntryClass e) {
    RegistryClass reg(e);
    RegCommandClass cmds();
    new RegCommandClass(cmds, "Command Two", RegCommandClass::READKEY,
      RegCommandClass::HKEY_CURRENT_USER, "", "", 0, 2);
    RegValueClass values();
    if (reg.Run(cmds, values)) {
      BookmarkClass folder(c.BookmarkRoot(), "Reg", NodeClass::FOLDER);
      BookmarkDataClass newData(folder);
      newData.SetRoot(values);
    }
  }

  void Main(CaseClass c) {
    if (c) {
      for(ItemIteratorClass iter(c); EntryClass e=iter.GetNextEntry();) {
        if (e.Name().Compare("ntuser.dat") == 0) {
          Console.WriteLine("Registry On " +  e.ItemPath());
          RunRegistry(c, e);
          break;
        }
      }
    }
    else
      Console.WriteLine("Need An Open Case");
  }
}
*/
